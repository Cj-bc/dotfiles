" ================== Plugin configs ========================

" " -------- TwitVim {{{
" let twitvim_browser_cmd = 'open'
" let twitvim_force_ssl = 1
" let twitvimount = 40
" }}}

" -------- tweetvim {{{
let g:tweetvim_silent_say = 1
let g:tweetvim_display_source = 1
nnoremap tww <esc>:TweetVimCommandSay<CR>
nnoremap twl <esc>:split +TweetVimHomeTimeline<CR>
nnoremap twt <esc>:split +Unite\ tweetvim/account<CR>
nnoremap twm <esc>:split +TweetVimMentions<CR>
" }}}

" -------- qiita.vim {{{
let g:qiita_vim_twitter = v:true
let g:qiita_vim_gist = v:false
let g:qiita_vim_private = v:false
" }}}

" -------- github-completion {{{
augroup config-github-complete
  autocmd!
  autocmd FileType gitcommit setl omnifunc=github_complete#complete
  autocmd FileType markdown setl omnifunc=github_complete#complete
  autocmd FileType conf  setl omnifunc=github_complete#complete
  autocmd FileType gfimarkdown setl omnifunc=github_complete#complete
augroup END
" }}}

" ------- github-issues.vim {{{
let g:github_issues_no_omni = 1 " set anything to disable omnifunc 
if filereadable(expand('~/.vimrc.private')) " source access token
  source ~/.vimrc.private
endif
" }}}

" ------- previm {{{
let g:previm_open_cmd = 'open -a Chrome'
augroup PrevimSettings
  autocmd!
  autocmd BufNewFile,BufRead *.{.md,mdwn,mkd,mkdn,mark*} set filetype=markdown
augroup END
" }}}

" -------- Qfixhowm {{{
let QFixHowm_FileType = 'markdown.vimwiki.qfix_memo'
let howm_dir = '~/howm'
let howm_filename            = '%Y/%m/%Y-%m-%d-%H%M%S.md'
let howm_fileencoding        = 'utf-8'

let HowmAlarmFile = howm_dir . '/howmalarm'
let HowmAlarmUseQFixHowm = 2

let SubWindow_Title1 = "__submenu1__"
" }}}

" ------- vimwiki {{{
au FileType vimwiki set noreadonly
let wiki_type_md = {'syntax': 'markdown', 'custom_wiki2html': '~/vimwiki/misaka_md2html.py', 'css_name': '~/vimwiki/template/style.css'}
let wiki_default = {'path': '~/vimwiki/wiki_default', 'path_html': '~/vimwiki/html/wiki_default'}
let wiki_note = {'path': '~/vimwiki/notes', 'path_html': '~/vimwiki/html/notes'}
let wiki_projects = {'path': '~/vimwiki/wiki_projects', 'path_html': '~/vimwiki/html/wiki_projects', 'css_name': '~/vimwiki/template/style.css'}
let wiki_tfc = { 'path': '~/Desktop/tfc/wiki', 'path_html': '~/Desktop/tfc/wiki_html/', 'css_name': '~/vimwiki/template/style.css'}
let wiki_codereading = { 'path': '~/vimwiki/wiki_codereading', 'path_html': '~/vimwiki/html/wiki_codereading'}
let wiki_howm = { 'path': howm_dir, 'path_html': howm_dir . '/html', 'syntax': 'markdown', 'css_name': '~/vimwiki/template/style.css'}
let wiki_yozakura_project = { 'path': '~/Desktop/dev/projects/yozakura-project/wiki', 'path_html': '~/Desktop/dev/projects/yozakura-project/wiki_html'}

call extend(wiki_codereading      , wiki_type_md)
call extend(wiki_yozakura_project , wiki_type_md)
call extend(wiki_note             , wiki_type_md)
call extend(wiki_default          , wiki_type_md)

let g:vimwiki_list = [wiki_default, wiki_note, wiki_projects, wiki_tfc, wiki_codereading, wiki_howm, wiki_yozakura_project]
let g:vimwiki_folding = 'expr'
" --- functions
" toggle syntax.
" source: http://tk2000ex.blogspot.com/2013/12/markdownvimwiki.html
" Vimwiki_syntax_toggle {{{2
function! Vimwiki_syntax_toggle()
  if ( &filetype == 'markdown')
    set filetype=vimwiki
  elseif ( &filetype == 'vimwiki' )
    set filetype=markdown
  endif
endfunction

" commit automatically when leaving wiki page
" dependencies: vim-fugitive
" Vimwiki_commit_automatic {{{2
function! Vimwiki_commit_automatic()
  if ( &filetype == 'vimwiki' )
    lcd `=vimwiki#vars#get_wikilocal('path')`
    if system('git status --short') != ""
      Git add *
      Git commit -m "Auto commit by Vimwiki_commit_automatic"
      redraw | echomsg 'commited.'
    else
      redraw | echomsg 'Nothing to commit.'
    endif
  endif
endfunction

" select notes dir by class name.
" Open top directory if no name is specified
" notes_select_class {{{2
function! s:notes_select_class(class_name)
  let s:wiki_num = match(g:vimwiki_list, "'path': '.*notes[^']*'")
  let s:index_file = vimwiki#vars#get_wikilocal('index') . vimwiki#vars#get_wikilocal('ext')
  if len(a:class_name) == 0
    call vimwiki#base#goto_index(s:wiki_num +1) " +1 because wiki's number start from 1, otherwize s:wiki_num start from 0
    set nofoldenable
  else
    let s:wiki_dir = vimwiki#vars#get_wikilocal('path', s:wiki_num) . a:class_name
    if isdirectory(s:wiki_dir)
      edit `=s:wiki_dir . '/' . s:index_file`
      let b:vimwiki_prev_link =  [vimwiki#vars#get_wikilocal('path', s:wiki_num) . s:index_file, [0, 0, 0, 0]]
      set nofoldenable
    else
      echohl Error | echomsg 'class: ' . a:class_name . ' is not defined.' | echohl None
    endif
  endif

endfunction
command! -nargs=? Notes :call s:notes_select_class("<args>")
" }}}

" autogroup {{{2
augroup Vimwiki_CustomCommands
  autocmd!
  autocmd BufNewFile,BufRead *.wiki nmap ,s :call Vimwiki_syntax_toggle()<CR>
  autocmd BufNewFile,BufRead *.wiki nmap vc :call Vimwiki_commit_automatic()<CR>
  autocmd QuitPre *.wiki call Vimwiki_commit_automatic()
augroup END
" }}}
" }}}

" ------- CtrlP {{{
let g:ctrlp_cmd = 'CtrlPMRU'
" }}}

" ------- DCSVS_mamanger {{{
let g:DCSVS_load_dragvisuals = 1
let g:DCSVS_load_betterdigraphs_utf8 = 1

"" ------- DVB {{{2
vmap  <expr>  <LEFT>   DVB_Drag('left')
vmap  <expr>  <RIGHT>  DVB_Drag('right')
vmap  <expr>  <DOWN>   DVB_Drag('down')
vmap  <expr>  <UP>     DVB_Drag('up')
vmap  <expr>  D        DVB_Duplicate()
" }}}
" }}}

" sonic-pi {{{
let g:sonicpi_command = 'sonic-pi-tool'
let g:sonicpi_send = 'eval-stdin'
" }}}

" openbrowser {{{
let g:openbrowser_browser_commands = [
                                     \ {"name": "w3m",
                                     \  "args": "tmux split-window 'w3m {uri}'"},
                                     \ {"name": "open",
                                     \  "args": ["open -a /Applications/Google Chrome.app", "{uri}"]}
                                     \]
" }}}

" translate.vim {{{
" functions below are not used for now
" let s:translate_lang_shortennames = { "e": "en",
"                                    \  "j": "ja"
"                                    \}
" function s:translate_detect_lang(a:shorten)
"   if ! has_key(s:translate_lang_shortennames, a:shorten)
"     return ''
"   endif
"
"   return s:translate_lang_shortennames[a:shorten]
" endfunction
vnoremap <leader>tlej :TranslateVisual en:ja<CR>
vnoremap <leader>tlje :TranslateVisual ja:en<CR>
nnoremap <leader>tlej :Translate en:ja<CR>
nnoremap <leader>tlje :Translate ja:en<CR>
nnoremap <leader>tlc :TranslateClear<CR>
" }}}

" vim-lsp {{{
augroup MyLsp
  "  for debug {{{2
  "    let g:lsp_log_verbose = 0
  "    "  let g:lsp_log_file = "vim-lsp.log"
  "    }}}

  if executable('ccls')
     au User lsp_setup call lsp#register_server({
        \ 'name': 'ccls',
        \ 'cmd': {server_info->['ccls']},
        \ 'root_uri': {server_info->lsp#utils#path_to_uri(lsp#utils#find_nearest_parent_file_directory(lsp#utils#get_buffer_path(), '.ccls'))},
        \ 'initialization_options': { 'cacheDirectory': '/tmp/ccls_cache' },
        \ 'whitelist': ['c', 'cpp', 'objc', 'objcpp', 'cc'],
        \ })
      au FileType c,cpp,objc,objcpp,cc call s:configure_lsp()
  endif
  if executable('pyls')
      au User lsp_setup call lsp#register_server({
        \ 'name': 'pyls',
        \ 'cmd': {server_info->['pyls']},
        \ 'whitelist': ['python'],
        \ })
      au FileType python call s:configure_lsp()
  endif

  if executable('hie')
      au User lsp_setup call lsp#register_server({
        \ 'name': 'hie',
        \ 'cmd': {server_info->[&shell, &shellcmdflag, 'hie --lsp']},
        \ 'whitelist': ['haskell'],
        \ })
      au FileType haskell call s:configure_lsp()
  endif

  func! s:configure_lsp()
      setlocal omnifunc=lsp#complete
      nmap <buffer> <leader>R <plug>(lsp-rename)
      nmap <buffer> <leader>D <plug>(lsp-definition)
      nmap <buffer> <leader>r <plug>(lsp-references)
      nmap <buffer> <leader>d <plug>(lsp-document-symbol)
      nmap <buffer> <leader>w <plug>(lsp-workspace-symbol)
  endfunc
augroup end
" }}}

" =================== Reference URLs =========================================
" ==                                                                        ==
" ==     		http://vim-jp.org                                               ==
" ==               VIM Japanese community site                              ==
" ==                                                                        ==
" ==          https://gitter.im/vim-jp/reading-vimrc                        ==
" ==               .vimrc Reading Community chat                            ==
" ==    http://tk2000ex.blogspot.com/2013/12/markdownvimwiki.html           ==
" ==        Vimwiki_syntax_toggle                                           ==
" ============================================================================
